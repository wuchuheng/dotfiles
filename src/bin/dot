#!/usr/bin/env zsh

source "${G_DOTFILES_ROOT}/src/lib/log.zsh"

TRUE=0
FALSE=1

function main() {
    local inputArgs=("$@")
    if [[ $# -eq 0 ]]; then
        local cache_file="${G_DOTFILES_ROOT}/src/cache/dot_output.txt"
        local cache_dir="${cache_file%/*}"
        [[ ! -d "$cache_dir" ]] && mkdir -p "$cache_dir"
        
        local needs_update=0
        if [[ ! -f "$cache_file" ]]; then
            needs_update=1
        else
            # Check if any file is newer than cache
            local newer=$(find "${G_DOTFILES_ROOT}/src/plugins" "${G_DOTFILES_ROOT}/src/workflows" -type f -name "*.zsh" -newer "$cache_file" -print -quit)
            if [[ -n "$newer" ]]; then
                needs_update=1
            fi
        fi

        if [[ $needs_update -eq 1 ]]; then
            {
                list_docs --path "src/plugins" --desc "List of cmd from Plugins" --ext "plugin" --exclude '${name}'
                list_docs --path "src/workflows" --desc "Workflows" --ext "workflow"
            } > "$cache_file"
        fi
        
        cat "$cache_file"
        # help
        return 0
    fi
    local command=$1
    case $command in
        help)
            help
            ;;
        p|plugins)
            list_docs --path "src/plugins" --desc "List of cmd from Plugins" --ext "plugin" --exclude '${name}'
            ;;
        workflows|w)
            list_docs --path "src/workflows" --desc "Workflows" --ext "workflow"
            ;;
        *)
            help
            ;;
    esac
    
}


##
# Print the help message for the command `dot`
#
function help() {
    cat <<EOF
Usage: dot <command> [options]
Commands:
  plugins , p: Print all the commands in the plugins.
  workflows , w: Print all the workflows.
Options:
  --help, -h: Print the help message.
EOF

}

function green_bold_print() {
    echo -e "\033[1;32m$1\033[0m"
}

function bold_print() {
    echo -e "\033[1m$1\033[0m"
}

##
# @Usage: list_docs --path [directory path] --desc [description of the list] --ext [file extension] --exclude [exclude match, default is empty]
# @Example: list_docs --path /path/to/plugins --desc "List all the cmd from plugins" --ext plugin
##
function list_docs() {
    typeset -A args=(
        [path]=''
        [desc]=''
        [ext]=''
        [exclude]=''
    )
    # 1 Parse the input arguments
    for (( i=1; i<=$#; i++ )); do
        local arg=${@[$i]}
        case $arg in
            --path)
                args[path]=${@[$((i+1))]}
                ;;
            --desc)
                args[desc]=${@[$((i+1))]}
                ;;
            --ext)
                args[ext]=${@[$((i+1))]}
                ;;
            --exclude)
                args[exclude]=${@[$((i+1))]}
                ;;
        esac
    done
    # 2 Check if the required arguments was provided or not.
    for key in "${(k)args[@]}"; do
        if [[ -z ${args[$key]} ]]; then
            if [[ $key == 'exclude' ]]; then
                continue
            fi
            echo "The argument $key is required."
            return 1
        fi
    done
    
    # 3 
    local _path="${G_DOTFILES_ROOT}/${args[path]}"
    local isPrintedTitle=$FALSE
    for name in $(ls ${_path}); do
        local entry="${_path}/${name}/${name}.${args[ext]}.zsh"
        if [[ -f $entry ]]; then
            local hasData=$( grep -E "^# @Docs:" $entry)
            if [[ -z $hasData ]]; then
                continue
            fi
            grep -E "^# @Docs:" $entry| while read line; do
                local cmdDocs=$( echo $line | sed -e 's/^# @Docs: //' )
                if [[ -n $cmdDocs ]]; then
                    if [[ $isPrintedTitle -eq $FALSE ]]; then
                        bold_print "${args[desc]}:"
                        isPrintedTitle=$TRUE
                    fi

                    local cmdName=${cmdDocs%%:*}
                    local cmdDesc=${cmdDocs#*:}

                    # Exclude the match. if the exclude match is provided and the cmdName matches the exclude match then skip the cmd.
                    if [[ -n ${args[exclude]} ]]; then
                        local excludeMatch=${args[exclude]}
                        if [[ $cmdName == *$excludeMatch* ]]; then
                            continue
                        fi
                    fi
                        echo "  $( green_bold_print ${cmdName}):${cmdDesc}"
                    
                fi
            done
        fi
    done
}

main $@